CMAIN
C
C     THIS IS A MAIN PROGRAM FOR LEAST SQUARES PROBLEMS
C     WITH INEQUALITY CONSTRAINTS.
C     THE SOLVER IS ENLSIP WRITTEN BY PER LINDSTROM, UNIVERSITY
C     OF UMEA, SWEDEN
C
C     NAME OF USER WRITTEN SUBROUTINES ARE FEX65 AND HEX65
C
      EXTERNAL FEX65,HEX65
      INTEGER N,M,P,L
      INTEGER EXIT
      INTEGER ACTIVE(10),WINT(36)
      DOUBLE PRECISION  
     *     H(7),X(3),F(10),WREAL(136)
      DOUBLE PRECISION
     *    BESTRK,BESTPG
      INTEGER NRREST,LATTRY
      COMMON /BACK/ BESTRK,BESTPG, NRREST,LATTRY
      MDI=36
      MDR=136
      NOUT=10
      N=3
      M=3
      P=0
      L=7
C
C     SET THE FIRST ELEMENTS OF THE ARRAYS WINT AND WREAL TO
C     SOMETHING NEGATIVE TO INDICATE THAT DEFAULT VALUES SHOULD
C     BE USED
C
      DO 10 I=1,6
           WINT(I)=-1
   10 CONTINUE
      DO 20 I=1,15
           WREAL(I)=-1.0
   20 CONTINUE
      X(1)=-5.0
      X(2)=5.0
      X(3)=0.0
C
C     DO SOME INITIAL PRINTING
C
      WRITE(NOUT,1020)
      WRITE(NOUT,1050) (X(I),I=1,N)
C
C     INVOKE ENLSIP TO MINIMIZE
C
      CALL ENLSIP(X,N,M,P,L,MDI,MDR,FEX65,HEX65,
     1 WINT,WREAL,EXIT,F,H,ACTIVE)
C
C     WRITE FINAL RESULTS
C
      CALL PRINT(NOUT,EXIT,ACTIVE,P,H,X,N,L,M,WINT,WREAL)
      STOP
1020  FORMAT(1H1///5X,28HHOCK,SCHITTKOWSKI PROBLEM 65)
1050  FORMAT(5X,14HSTARTING POINT,5X,3E12.3)
      END
CPRINT
      SUBROUTINE PRINT(NOUT,EXIT,ACTIVE,P,H,X,N,L,M,WINT,WREAL)
      INTEGER NOUT,EXIT,P,N,L,M
      INTEGER ACTIVE(P),WINT(13)
      DOUBLE PRECISION  
     *     H(L),X(N),WREAL(7)
C
C     PRINT FINAL RESULTS ON UNIT NOUT
C
      WRITE(NOUT,1000) EXIT,WINT(7),WREAL(7)
1000  FORMAT(///1X,5HEXIT=,I8,22H    NO. OF ITERATIONS=,I5,
     1 8H  SPEED=,E12.3)
      WRITE(NOUT,1010) WINT(12),P,WINT(13),M
1010  FORMAT(1X,6HRANKA=,I4,5H   P=,I4,8H   RANK=,I4,5H   M=,I4)
      WRITE(NOUT,1020) WINT(8),WINT(9),WINT(10),WINT(11)
1020  FORMAT(1X,7HFUNCEV=,I5,8H  JACEV=,I5,8H  SECEV=,I5,
     1 8H  LINEV=,I5)
      FSUM=2.0*WREAL(6)
      FL2NRM=SQRT(FSUM)
      HSUM=0.0
      IF(P.LE.0) GOTO 20
      DO 10 I=1,P
           K=ACTIVE(I)
           HSUM=HSUM+H(K)**2
   10 CONTINUE
   20 CONTINUE
      WRITE(NOUT,1030) WREAL(6),FSUM,FL2NRM,HSUM
1030  FORMAT(1X,7HPHI(X)=,E15.6,7H  FSUM=,E15.6,9H  FL2NRM=,E15.6,
     1 7H  HSUM=,E12.3)
      WRITE(NOUT,1040) (X(I),I=1,N)
1040  FORMAT(1X,12HAT THE POINT/(1X,6E16.8))
      IF(P.LE.0) GOTO 30
      WRITE(NOUT,1050) (ACTIVE(I),I=1,P)
1050  FORMAT(5X,18HACTIVE CONSTRAINTS,(30I3))
   30 CONTINUE
      IF(L.LE.0) GOTO 40
      WRITE(NOUT,1060) (H(I),I=1,L)
1060  FORMAT(5X,20HCONSTRAINT VALUES     /(5X,8E12.3))
      WRITE(NOUT,1070) (WREAL(I+7),I=1,L)
1070  FORMAT(5X,20HPENALTY CONSTANTS     /(5X,8E12.3))
C
C     PRINT DEFAULT VALUES RETURNED IN WINT AND WREAL
C
   40 CONTINUE
      WRITE(NOUT,1080) (WINT(I),I=1,4)
 1080 FORMAT(20X,14HDEFAULT VALUES/20X,7HIPRINT=,I3/20X,
     1   5HNOUT=,I3/20X,6HMAXIT=,I4/20X,5HNORM=,I3)
      WRITE(NOUT,1090) (WREAL(I),I=1,5)
 1090 FORMAT(20X,4HTOL=,E15.6/20X,7HEPSREL=,E15.6/20X,7HEPSABS=,
     1   E15.6/20X,5HEPSX=,E15.6/20X,5HEPSH=,E15.6)
      RETURN
      END
CFEX65
      SUBROUTINE FEX65(X,N,F,M,CTRL,C,MDC)
      INTEGER N,M,CTRL,MDC
      DOUBLE PRECISION  
     *     X(N),F(M),C(MDC,N)
C
C     THIS IS AN EXAMPLE OF A USER WRITTEN SUBROUTINE FOR EVALUATING
C     THE RESIDUALS AND THE CORRESPONDING JACOBIAN. IT SHOULD BE
C     USED IN THE CONSTRAINED NONLINEAR LEAST SQUARES SOLVER ENLSIP
C     (WRITTEN BY PER LINDSTROM, UNIVERSITY OF UMEA, SWEDEN)
C
C     THE ACTUAL TEST EXAMPLE IS PROBLEM NO. 65 GIVEN BY HOCK AND
C     SCHITTKOWSKI IN "TEST EXAMPLES FOR NONLINEAR PROGRAMMING CODES",
C     LECTURE NOTES IN ECONOMICS AND MATHEMATICAL SYSTEMS 187,
C     SPRINGER-VERLAG HEIDELBERG (1981)
C
C     THE PROBLEM TO SOLVE IS
C
      IF(IABS(CTRL).NE.1) GOTO 20
C
C     EVALUATE THE RESIDUALS
C
      F(1)=X(1)-X(2)
      F(2)=(X(1)+X(2)-10.0)/3.0
      F(3)=X(3)-5.0
      RETURN
   20 CONTINUE
C
C     THE JACOBIAN IS COMPUTED ANALYTICALLY
C
      DO 40 I=1,N
           DO 30 J=1,N
             C(I,J)=0.0
   30      CONTINUE
      C(I,I)=1.0
   40 CONTINUE
      C(1,2)=-1.0
      C(2,1)=1.0/3.0
      C(2,2)=1.0/3.0
      RETURN
      END
CHEX65
      SUBROUTINE HEX65(X,N,H,L,CTRL,A,MDA)
      INTEGER N,L,CTRL,MDA
      DOUBLE PRECISION  
     *     X(N),H(L),A(MDA,N)
C
C     THIS IS AN EXAMPLE OF A USER WRITTEN SUBROUTINE FOR EVALUATING
C     THE CONSTRAINTS AND THE CORRESPONDING JACOBIAN. IT SHOULD BE
C     USED IN THE CONSTRAINED NONLINEAR LEAST SQUARES SOLVER ENLSIP
C     (WRITTEN BY PER LINDSTROM, UNIVERSITY OF UMEA, SWEDEN)
C
C     THE ACTUAL TEST EXAMPLE IS PROBLEM NO. 65 GIVEN BY
C     HOCK AND SCHITTKOWSKI
C
      IF(IABS(CTRL).NE.1) GOTO 10
C
C     EVALUATE THE CONSTRAINTS
C
      H(1)=48.0-X(1)**2-X(2)**2-X(3)**2
      H(2)=X(1)+4.5
      H(3)=X(2)+4.5
      H(4)=X(3)+5.0
      H(5)=-X(1)+4.5
      H(6)=-X(2)+4.5
      H(7)=-X(3)+5.0
      RETURN
   10 CONTINUE
C
C     THE JACOBIAN IS COMPUTED NUMERICALLY BY THE PACKAGE
C     IF CTRL IS SET TO ZERO ON RETURN
C
      CTRL=0
      RETURN
      END
